buildscript {
    repositories {
        jcenter()
        google()
        maven { url "https://jitpack.io" }
    }
    dependencies {
        classpath 'com.github.timfreiheit:ResourcePlaceholdersPlugin:0.2'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'de.timfreiheit.resourceplaceholders.plugin'

if (!project.hasProperty("MyWarwick.keystore")) {
    throw new Exception("~/.gradle/gradle.properties needs to contain the necessary MyWarwick.* properties.");
}

def buildNumber = 33

android {
    signingConfigs {
        myWarwickPlay {
            keyAlias project.property("MyWarwick.keyAlias")
            keyPassword project.property("MyWarwick.keyPass")
            storeFile file(project.property("MyWarwick.keystore"))
            storePassword project.property("MyWarwick.keyPass")
        }
    }

    compileSdkVersion 26

    defaultConfig {
        applicationId "uk.ac.warwick.my.app"
        minSdkVersion 19
        targetSdkVersion 26
        versionCode buildNumber
        versionName buildNumber + ""
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.myWarwickPlay
            resValue "string", "app_name", "My Warwick"
            resValue "string", "package_name", android.defaultConfig.applicationId
        }
        debug {
            applicationIdSuffix ".debug"
            resValue "string", "app_name", "My Warwick dev"
            resValue "string", "package_name", android.defaultConfig.applicationId + applicationIdSuffix
        }
    }
}

dependencies {
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:1.10.19'

    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support:design:26.1.0'
    implementation 'com.android.support:customtabs:26.1.0'
    implementation 'com.android.support:cardview-v7:26.1.0'

    implementation 'com.google.firebase:firebase-crash:11.8.0'
    implementation 'com.google.firebase:firebase-messaging:11.8.0'

    implementation 'com.roughike:bottom-bar:2.3.1'
    implementation 'com.google.guava:guava:24.0-android'

    implementation 'com.github.apl-devs:appintro:v4.2.0'

    implementation 'com.squareup.okhttp3:okhttp:3.9.0'

    implementation 'com.github.bumptech.glide:glide:4.0.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.0.0'

    androidTestImplementation 'com.android.support.test.espresso:espresso-web:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
}

task archiveProguardMapping(type: Sync) {
    from 'build/outputs/mapping/release'
    into "artifacts/build-${buildNumber}/mapping"
}

tasks.whenTaskAdded { task ->
    if (task.name == 'assembleRelease') {
        task.dependsOn archiveProguardMapping
    }

    if (task.name == 'transformClassesAndResourcesWithProguardForRelease') {
        archiveProguardMapping.mustRunAfter task
    }
}

resourcePlaceholders {
    // So that ${applicationId} is replaced with the app ID
    files = ['xml-v25/shortcuts.xml']
}

// This needs to be at the bottom because of reasons:
// https://developers.google.com/android/guides/google-services-plugin
apply plugin: 'com.google.gms.google-services'
