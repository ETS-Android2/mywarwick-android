apply plugin: 'com.android.application'

if (!project.hasProperty("MyWarwick.keystore")) {
    throw new InvalidUserDataException("~/.gradle/gradle.properties needs to contain the necessary MyWarwick.* properties.");
}

def buildNumber = 13
def buildVersionSuffix = " - Beta build"

android {
    signingConfigs {
        myWarwickPlay {
            keyAlias project.property("MyWarwick.keyAlias")
            keyPassword project.property("MyWarwick.keyPass")
            storeFile file(project.property("MyWarwick.keystore"))
            storePassword project.property("MyWarwick.keyPass")
        }
    }
    compileSdkVersion 25
    buildToolsVersion '25.0.2'

    defaultConfig {
        applicationId "uk.ac.warwick.my.app"
        minSdkVersion 15
        targetSdkVersion 25
        versionCode buildNumber
        versionName buildNumber + buildVersionSuffix
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.myWarwickPlay
        }
        debug {
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'

    compile 'com.android.support:appcompat-v7:25.2.0'
    compile 'com.android.support:design:25.2.0'
    compile 'com.android.support:cardview-v7:25.2.0'

    compile 'com.roughike:bottom-bar:2.1.1'
    compile 'com.google.guava:guava:20.0'

    compile 'com.google.firebase:firebase-messaging:10.2.0'
}

apply plugin: 'com.google.gms.google-services'